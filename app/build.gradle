apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    signingConfigs {
        release {
            keyAlias 'release'
            keyPassword '***REMOVED***'
            storeFile file('../debug.jks')
            storePassword '***REMOVED***'
        }
    }
    defaultConfig {
        applicationId "ru.softstone.kotime"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 3
        versionName "0.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            def buildType = variant.variantData.variantConfiguration.buildType.name
            def version = variant.versionName
            def date = new Date()
            def formattedDate = date.format('ddMMyy')
            outputFileName = "kotime_${buildType}_${version}_${formattedDate}.apk"
        }
    }
}
androidExtensions {
    experimental = true // for @Parcelize
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "com.google.android.material:material:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'


    // RxJava2
    implementation "io.reactivex.rxjava2:rxjava:2.2.9"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // Dagger2
    def dagger_version = "2.16"
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // Moxy
    def moxy_version = "1.5.3"
    implementation "com.arello-mobile:moxy:$moxy_version"
    implementation "com.arello-mobile:moxy-app-compat:$moxy_version"
    kapt "com.arello-mobile:moxy-compiler:$moxy_version"

    // Cicerone
    implementation 'ru.terrakok.cicerone:cicerone:3.0.0'

    // Room
    def room_version = "2.2.2"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    testImplementation "android.arch.persistence.room:testing:$room_version"

    // Epoxy
    def epoxy_version = "3.1.0"
    implementation "com.airbnb.android:epoxy:$epoxy_version"
    kapt "com.airbnb.android:epoxy-processor:$epoxy_version"

    // Material Dialogs
    // не обновлять до 2.7.0. там бага, календарь не помещается в диалог на экране 360dp
    def dialog_version = "2.6.0"
    //noinspection GradleDependency
    implementation "com.afollestad.material-dialogs:core:$dialog_version"
    //noinspection GradleDependency
    implementation "com.afollestad.material-dialogs:datetime:$dialog_version"

    // Google Play Billing
    implementation 'com.android.billingclient:billing:1.2.2'

    // Color Picker
    implementation "com.github.skydoves:colorpickerview:2.1.5"
}

kapt {
    correctErrorTypes = true // Epoxy requirement
}
